apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile = file("../../key.jks")
            storePassword = System.getenv("RELEASE_KEYSTORE_PASSWORD")
            keyAlias = System.getenv("RELEASE_KEY_ALIAS")
            keyPassword = System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "de.vincentscode.stickerapp"
        minSdkVersion 21
        compileSdk 35
        targetSdkVersion 35
        versionCode 13
        versionName "13.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def contentProviderAuthority = applicationId + ".stickercontentprovider"
        // Creates a placeholder property to use in the manifest.
        manifestPlaceholders =
                [contentProviderAuthority: contentProviderAuthority]
        // Adds a new field for the authority to the BuildConfig class.
        buildConfigField("String",
                "CONTENT_PROVIDER_AUTHORITY",
                "\"${contentProviderAuthority}\"")
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = "17"
        targetCompatibility = "17"
    }

    packagingOptions {
        jniLibs {
            excludes += ['lib/*/libnative-imagetranscoder.so', 'lib/*/libnative-filters.so']
        }
    }

    androidResources {
        noCompress 'webp'
    }

    namespace 'de.vincentscode.stickerapp'
    buildFeatures {
        buildConfig true
    }

    compileSdk 35
    buildToolsVersion '35.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.facebook.infer.annotation:infer-annotation:0.18.0'
    implementation "com.facebook.fresco:fresco:3.6.0"
    implementation "com.facebook.fresco:webpsupport:3.6.0"
    implementation "com.facebook.fresco:animated-webp:3.6.0"
    implementation "com.facebook.fresco:animated-base:3.6.0"
}
